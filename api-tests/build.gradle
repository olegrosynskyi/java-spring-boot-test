dependencies {
    testImplementation libraries.karate_apache
    testImplementation libraries.karate_junit5
    testImplementation libraries.cucumber_reporting
    testImplementation libraries.commons_lang3
    testImplementation libraries.kenshoo_auth
    testImplementation libraries.jaxb
}

tasks.withType(Test) {
    systemProperty "karate.env", System.properties.getProperty("karate.env")
    useJUnitPlatform()
    outputs.upToDateWhen { false }
    testLogging.showStandardStreams = true
    doFirst {
        // load Vault secrets so that they're available to Karate tests:
        def props = new Properties()
        file("${project.rootDir}/.env").withInputStream { props.load(it) }
        props.entrySet().forEach {entry -> systemProperty entry.key, entry.value }
    }
}

test {
    def karateGroup = System.properties.getProperty('testGroups', 'ApiTest')
    systemProperty "karate.options", System.properties.getProperty("karate.options", "") + " --tags " + karateGroup
}

task smokeTest(type: Test) {
    systemProperty "karate.options", System.properties.getProperty("karate.options", "") + " --tags SmokeTest"
    systemProperty "url", System.properties.getProperty("url")
}

sourceSets {
    test {
        resources {
            srcDir file('src/test/java')
            exclude '**/*.java'
        }
    }
}