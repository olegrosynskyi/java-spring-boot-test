subprojects {
    apply plugin: "jacoco"
    apply plugin: "java"
    jacoco {
        toolVersion = "0.8.7"
        reportsDirectory = file("$buildDir/customJacocoReportDir")
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    def jacocoExclude = [
            'io/skai/**/*Config.class',
            'io/skai/**/*Configuration.class',
    ]

    jacocoTestReport {
        dependsOn test
        finalizedBy jacocoTestCoverageVerification
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: jacocoExclude)
            }))
        }
    }

    jacocoTestCoverageVerification {
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: jacocoExclude)
            }))
        }
        violationRules {
            rule {
                limit {
                    minimum = 0.7
                }
            }

            rule {
                enabled = false
                element = 'CLASS'
                includes = ['org.gradle.*']

                limit {
                    counter = 'LINE'
                    value = 'TOTALCOUNT'
                    maximum = 0.3
                }
            }
        }
    }
}