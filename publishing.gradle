apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    repositories {
        maven {
            credentials {
                username = System.env.MICROSERVICES_ARTIFACTORY_USER
                password = System.env.MICROSERVICES_ARTIFACTORY_PASSWORD
            }
            url "https://kenshoo.jfrog.io/kenshoo/simple/libs-releases-local/"
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.kenshoo'
            version "$project.version"

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
        all {
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each { it.scope*.value = 'compile' }
            }
        }
    }
}
